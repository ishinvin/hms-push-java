plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'signing'
}

group = 'io.github.ishinvin.push'
version = System.getenv("GITHUB_RELEASE_VERSION") ?: "0.0.1-SNAPSHOT"
description = 'hms-push-java'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.guava:guava:33.3.0-jre'
    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation 'com.alibaba:fastjson:2.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    testImplementation 'junit:junit:4.13.1'
}

checkstyle {
    toolVersion = "10.16.0"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'HMS Push Java'
                description = 'push notification with HMS Push Kit'
                url = 'https://github.com/ishinvin/hms-push-java'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'io.github.ishinvin'
                        name = 'Ishin Vin'
                        email = 'ishinvin26@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(findProperty("signing.keyId") as String, findProperty("signing.password") as String)
    sign publishing.publications.mavenJava
}
